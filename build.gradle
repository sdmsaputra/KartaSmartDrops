plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

version = '2.17'

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven { url "https://mvn-repo.arim.space/lesser-gpl3/" }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    compileOnly 'org.spigotmc:spigot-api:1.16.1-R0.1-SNAPSHOT'
    compileOnly 'commons-io:commons-io:2.11.0'

    compileOnly 'org.jetbrains:annotations:21.0.1'

    implementation 'space.arim.morepaperlib:morepaperlib:0.4.3'
}

processResources {
    filesMatching('plugin.yml') {
        expand(version: project.version)
    }
}

shadowJar {
    relocate 'space.arim.morepaperlib', 'com.demkom58.divinedrop.libs.morepaperlib'
}

rootProject.tasks.named("processResources") {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveBaseName.set("DivineDrop")
    archiveClassifier.set(null)
}

jar {
    archiveBaseName.set("DivineDrop")
    archiveClassifier.set("thin")
}

tasks.build {
    dependsOn shadowJar
}